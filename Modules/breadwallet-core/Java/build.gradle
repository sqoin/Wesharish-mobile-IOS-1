buildscript {
    ext.androidConfig = [
        'compileSdkVersion':        28,
        'minSdkVersion':            23,
        'targetSdkVersion':         28,
    ]
    ext.versions = [
        'androidAppCompatV7':       '28.0.0',
        'androidConstraintLayout':  '1.1.3',
        'androidRecyclerView':      '28.0.0',
        'androidTestRunner':        '1.0.2',
        'androidTestEspresso':      '3.0.2',
        'guava':                    '28.1',
        'okhttp':                   '4.2.0',
        'jackson':                  '2.10.1',
        'jna':                      '5.4.0',
        'junit':                    '4.12',
    ]
    ext.deps = [
        'androidAppCompatV7':       "com.android.support:appcompat-v7:${versions.androidAppCompatV7}",
        'androidConstraintLayout':  "com.android.support.constraint:constraint-layout:${versions.androidConstraintLayout}",
        'androidRecyclerView':      "com.android.support:recyclerview-v7:${versions.androidRecyclerView}",
        'androidTestRunner':        "com.android.support.test:runner:${versions.androidTestRunner}",
        'androidTestEspresso':      "com.android.support.test.espresso:espresso-core:${versions.androidTestEspresso}",
        'guavaAndroid':             "com.google.guava:guava:${versions.guava}-android",
        'guavaJre':                 "com.google.guava:guava:${versions.guava}-jre",
        'okhttp':                   "com.squareup.okhttp3:okhttp:${versions.okhttp}",
        'jacksonCore':              "com.fasterxml.jackson.core:jackson-core:${versions.jackson}",
        'jacksonAnnotations':       "com.fasterxml.jackson.core:jackson-annotations:${versions.jackson}",
        'jacksonDatabind':          "com.fasterxml.jackson.core:jackson-databind:${versions.jackson}",
        'jacksonDatabindGuava':     "com.fasterxml.jackson.datatype:jackson-datatype-guava:${versions.jackson}",
        'jnaAndroid':               "net.java.dev.jna:jna:${versions.jna}@aar",
        'jnaJre':                   "net.java.dev.jna:jna:${versions.jna}",
        'junit':                    "junit:junit:${versions.junit}",
    ]
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.2'
        classpath 'digital.wup:android-maven-publish:3.6.2'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
    afterEvaluate {
        def publishing = extensions.findByType(PublishingExtension)
        if (publishing != null) {
            publishing.repositories {
                maven {
                    credentials {
                        username project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
                        password project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
                    }
                    url "$maven_url/${project.name}/"
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
